plugins {
	id 'fabric-loom' version '1.7-SNAPSHOT'
	id 'maven-publish'
	id 'com.matthewprenger.cursegradle' version '1.+'
	id "com.modrinth.minotaur" version "2.+"
}

apply plugin: "com.modrinth.minotaur"
apply plugin: "com.matthewprenger.cursegradle"

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

repositories {
	mavenLocal()
	mavenCentral()
	maven {
		name "SquidDev"
		url "https://squiddev.cc/maven"
	}
	maven { url 'https://jitpack.io' }
	maven { url "https://maven.shedaniel.me/" }
	maven { url "https://maven.terraformersmc.com/releases" }
	maven { url "https://maven.pitan76.net/" }
}

dependencies {
	// To change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	// Fabric API. This is technically optional, but you probably want it anyway.
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

	// TRのエネルギー用
	modImplementation 'RebornCore:RebornCore-1.21:5.11.10'
	modImplementation 'TechReborn:TechReborn-1.21:5.11.10'

	modImplementation "me.shedaniel:RoughlyEnoughItems-fabric:16.0.754"

	// CC: Tweaked
	//modImplementation("cc.tweaked:cc-tweaked-1.21-fabric-api:${project.cc_version}")
	//modApi("cc.tweaked:cc-tweaked-1.21-fabric:${project.cc_version}")

	// Mod Menu
	//modApi "com.terraformersmc:modmenu:2.0.10"

	// Cloth Config API
	modApi("me.shedaniel.cloth:cloth-config-fabric:15.0.130") {
		exclude(group: "net.fabricmc.fabric-api")
	}

	modImplementation "dev.architectury:architectury-fabric:${rootProject.architectury_version}"
	modImplementation("net.pitan76:mcpitanlib-fabric${rootProject.mcpitanlib_version}") {
		exclude(group: "dev.architectury")
	}
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	// ensure that the encoding is set to UTF-8, no matter what the system default is
	// this fixes some edge cases with special characters not displaying correctly
	// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
	// If Javadoc is generated, this must be specified in that task too.
	it.options.encoding = "UTF-8"

	// The Minecraft launcher currently installs Java 8 for users, so your mod probably wants to target Java 8 too
	// JDK 9 introduced a new way of specifying this that will make sure no newer classes or methods are used.
	// We'll use that if it's available, but otherwise we'll use the older option.
	def targetVersion = 17
	if (JavaVersion.current().isJava9Compatible()) {
		it.options.release = targetVersion
	}
}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.archivesBaseName}"}
	}
}

// configure the maven publication
publishing {
	publications {
		mavenJava(MavenPublication) {
			// add all the jars that should be included when publishing to maven
			artifact(remapJar) {
				builtBy remapJar
			}
			artifact(sourcesJar) {
				builtBy remapSourcesJar
			}
		}
	}

	// See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
	repositories {
		// Add repositories to publish to here.
		// Notice: This block does NOT have the same function as the block in the top level.
		// The repositories here will be used for publishing your artifact, not for
		// retrieving dependencies.
	}
}

if (System.getenv("CURSEFORGE_TOKEN") != null) {
	curseforge {
		apiKey = System.getenv("CURSEFORGE_TOKEN")
		project {
			id = '543089'
			changelog = project.changelog
			releaseType = 'release'
			addGameVersion "Fabric"
			addGameVersion project.minecraft_version
			relations {
				requiredDependency "fabric-api"
				requiredDependency "techreborn"
				requiredDependency "reborncore"
				requiredDependency "mcpitanlibarch"
				optionalDependency "roughly-enough-items"
				optionalDependency "cc-tweaked"
			}

			mainArtifact(tasks.remapJar)
		}
	}
}

if (System.getenv("MODRINTH_TOKEN") != null) {
	modrinth {
		token = System.getenv("MODRINTH_TOKEN")
		projectId = 'ir8okcEA'
		versionNumber = project.mod_version + "-fabric"
		gameVersions = [project.minecraft_version]
		versionType = 'release'
		uploadFile = tasks.remapJar
		changelog = project.changelog
		loaders = ["fabric"]
		dependencies {
			required.project "P7dR8mSH" // Fabric API
		}
	}
}